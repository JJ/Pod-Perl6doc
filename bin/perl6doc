#!/usr/bin/env perl6
use v6;

my class Cache {
    has $!dir = %*ENV<HOME> ~ "/.perl6doc";
    method !key(Str $file is copy) {
        $file ~~ s:i:g/<-[a..z0..9.]>/_/;
        "$!dir/$file";
    }
    method get(Str $file) {
        my $key = self!key($file);
        if $key.IO.e {
            if $file.IO.modified <= $key.IO.modified {
                return $key;
            } else {
                $key.unlink;
            }
        }
        return;
    }
    method set(Str $file, Str:D $content) {
        return if $content eq "";
        my $key = self!key($file);
        my $d = $key.IO.dirname;
        unless $d.IO.d {
            require Shell::Command;
            GLOBAL::Shell::Command::EXPORT::DEFAULT::<&mkpath>($d);
        }
        $key.IO.spurt: $content;
        $key;
    }
    method compute(Str $file, &cb) {
        my $get = self.get($file);
        return $get if $get;
        my $content = &cb();
        self.set($file, $content);
    }
}

my class PerlFile {
    has $!module;
    has $.pod;
    has $.pm;
    submethod BUILD(:$module) {
        my $path = (-> $_ { .subst(:g, '::', '/') })($module);
        for @*INC -> $inc is copy {
            next unless $inc ~~ Str;
            $inc ~~ s/^.*\#//;
            my @found = (
                <pm6 pm pod>.map({"$inc/$path.$_"}).Slip,
                "$inc/Type/$path.pod",
                "$inc/Language/$path.pod",
            ).grep({.IO.r});
            next unless @found;
            if @found.grep(/pod$/) -> @f {
                # prefer *.pod for pod
                $!pod = @f[0];
            } else {
                $!pod = @found[0];
            }
            $!pm = @found[0];
            last;
        }
    }
    method found() { so $!pm }
}

multi sub MAIN(Str $module, Bool :$l, Bool :$m) {
    my $perl-file = PerlFile.new(:$module);
    if $perl-file.found {
        if $l {
            say $perl-file.pm;
        } elsif $m {
            with-pager($perl-file.pm);
        } else {
            my $cache = Cache.new;
            my $cache-file = $cache.compute: $perl-file.pod, {
                qqx/$*EXECUTABLE --doc=TextPodPerl6doc {$perl-file.pod}/;
            };
            unless $cache-file {
                note qq/File exists, but no documentation found for "$module"./;
                exit 1;
            }
            with-pager($cache-file);
        }
    } else {
        my $message = $m ?? qq/No module found for "$module"./
                         !! qq/No documentation found for "$module"./;
        note $message;
        exit 1;
    }
}

sub USAGE() {
    require Pod::To::Text;
    say ::("Pod::To::Text").render($=pod);
}

sub with-pager($file) {
    my $pager = $*ENV<PAGER>:exists ?? $*ENV<PAGER> !! "less";
    temp %*ENV<LESS> = %*ENV<LESS>:exists ?? "%*ENV<LESS> -R" !! "-R" if $pager ~~ /less$/;
    shell "$pager $file";
}

=begin pod

=head1 SYNOPSIS

  > perl6doc [OPTIONS] MODULE

  OPTIONS:
  -l  Display the module's file name
  -m  Display module's file in its entirety
  -h  Show this help

  EXAMPLES:
  > perl6doc File::Find
  > perl6doc -m Panda::Builder
  > perl6doc -l Pod::To::Text

  # for perl6/doc content
  > perl6doc Proc::Async
  > perl6doc 5to6-perlfunc

=end pod
